plugins {
    id "com.glovoapp.semantic-versioning" version "1.1.10"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()

        modCompileOnly "me.shedaniel:REIPluginCompatibilities-forge-annotations:9+"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.version
    group = rootProject.maven_group

    repositories {
        mavenLocal()

        maven {
            url "https://maven.impleri.org/minecraft"
        }

        maven { url "https://maven.shedaniel.me" }

        maven {
            url "https://maven.architectury.dev"
            content {
                includeGroup "dev.architectury"
            }
        }
        maven { url "https://maven.blamejared.com" }

        maven {
            url "https://maven.saps.dev/minecraft"
            content {
                includeGroup "dev.latvian.mods"
            }
        }

        maven {
            // location of the maven that hosts JEI files
            name "Progwml6 maven"
            url "https://dvs1.progwml6.com/files/maven/"
        }
        maven {
            // location of a maven mirror for JEI files, as a fallback
            name "ModMaven"
            url "https://modmaven.dev"
        }

        mavenCentral()
    }

    publishing {
        repositories {
            maven {
                name = "impleri-dev"
                url = uri("https://maven.impleri.org/minecraft")
                credentials {
                    username = System.getenv("MAVEN_USER")
                    password = System.getenv("MAVEN_TOKEN")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
